buildscript {
    ext {
        queryDslVersion = "5.0.0"
    }
}

plugins {
    id 'java'
    id 'org.springframework.boot' version '2.7.11'
    id 'io.spring.dependency-management' version '1.0.15.RELEASE'
    
    // querydsl 추가
    id 'com.ewerk.gradle.plugins.querydsl' version '1.0.10'
}

group = 'chat'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-websocket'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework:spring-messaging'
//    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-validation'

    // QueryDSL
    implementation "com.querydsl:querydsl-jpa:$queryDslVersion"
    annotationProcessor "com.querydsl:querydsl-apt:$queryDslVersion"

    // 타임리프에서 스프링시큐리티의 문법이나 형식을 지원하는 확장팩 라이브러리
    implementation 'org.thymeleaf.extras:thymeleaf-extras-springsecurity5'

    // Lombok
    testImplementation 'org.projectlombok:lombok:1.18.22'
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'

    developmentOnly 'org.springframework.boot:spring-boot-devtools'

    // chatgpt
    implementation 'io.github.flashvayne:chatgpt-spring-boot-starter:1.0.4'

    // Object to byteArray, StringUtils.replace
    implementation 'org.apache.commons:commons-lang3:3.0'

    // mysql
    runtimeOnly 'com.mysql:mysql-connector-j'

    // log4jdbc
    implementation 'org.bgee.log4jdbc-log4j2:log4jdbc-log4j2-jdbc4.1:1.16'

    // MapStruct https://mvnrepository.com/artifact/org.mapstruct/mapstruct
    implementation 'org.mapstruct:mapstruct:1.5.3.Final'

    // MapStruct https://mvnrepository.com/artifact/org.mapstruct/mapstruct-processor
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.3.Final'

    // RestTemplate https://mvnrepository.com/artifact/org.apache.httpcomponents/httpclient
    implementation 'org.apache.httpcomponents:httpclient:4.5.13'

    // @ConfigurationProperties ChatGpt 관련 옵션으로 사용하는듯
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
}

test {
    useJUnitPlatform()
}

// QueryDSL 추가 시작
def querydslDir = "$buildDir/generated/querydsl"
querydsl {
    jpa = true
    querydslSourcesDir = querydslDir
}
sourceSets {
    main {
        java {
            srcDir querydslDir
        }
    }
}
configurations {
    querydsl {
        extendsFrom compileClasspath
    }
}

compileQuerydsl {
    options.annotationProcessorPath = configurations.querydsl
}
// QueryDSL 끝

// AWS Elastic Beanstalk 배포를 위한 빌드
task buildAwsEb(type: Zip) {
    from bootJar.outputs.files
    from('.ebextensions') {
        into('.ebextensions')
    }
    from('.platform') {
        into('.platform')
    }
    archiveFileName = 'myproject.zip'
    destinationDirectory = file('build/libs')
}